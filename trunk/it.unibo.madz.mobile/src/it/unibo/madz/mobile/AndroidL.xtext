grammar it.unibo.madz.mobile.AndroidL with org.eclipse.xtext.common.Terminals

generate androidL "http://www.unibo.it/madz/mobile/AndroidL"

AndroidSystem : 
	(data+=Data)*
	(action+=Action)*
	(serviceInterface=ServiceInterface)* 
	(component+=Component)* 
	application=Application ;

//------------------------------------------- 
// Component 
//------------------------------------------- 
Component :
	Activity | Service ;

//------------------------------------------- 
// Service Interface
//------------------------------------------- 
ServiceInterface : 
	"serviceInterface" name=ID 
	( operation += LocalOperation )* ;
	
LocalOperation : 
	"op" name=ID 
	"return" return = STRING 
	( "inputType" inputType += InputType ";" )* ;
	
enum InputType : String | int | double ;

//------------------------------------------- 
// Service
//------------------------------------------- 
Service : 
	"Service" name=ID 
	"implements" interface = [ ServiceInterface ]
	( serviceInteraction += ExplicitInteractionService )* ;


//------------------------------------------- 
// Activity 
//------------------------------------------- 
Activity : 
	"Activity" name=ID 
	( launchable?='launchable' )? 
	( actions += LocalAction )* 
	( interactions += InterAction )* ;	
	
//------------------------------------------- 
// LocalAction 
//------------------------------------------- 
LocalAction : 
	"action" action = [ Action ] 
	(button?="button")? ;
	
//------------------------------------------- 
// InterAction 
//------------------------------------------- 
InterAction : 
	ExplicitInteraction	(button?="button")? | 
	ActionOnlyInteraction (button?="button")? ;

//------------------------------------------- 
// ExplicitInteraction 
//------------------------------------------- 
ExplicitInteraction : 
	ExplicitInteractionActivity |
	ExplicitInteractionService ;
	
ExplicitInteractionActivity : 
	"callActivity" dest = [ Activity ] 
	( "forActiom" action = [ Action ] )? ;
	
ExplicitInteractionService : 
	UseService | 
	Launchservice ;
	
UseService : 
	"useService" dest = [ ServiceInterface ] 
	"forOp" operation = [ LocalOperation ] 
	( "arg=" arg += STRING	)* ; 

Launchservice : 
	"launchService" dest = [ Service ] ;
	
ActionOnlyInteraction : 
	"execAction" action = [ Action ] ;

//------------------------------------------- 
// Action 
//------------------------------------------- 
Action : 
	"action" name=ID 
	"data" data=[ Data ] 
	"category" category=STRING ;

//------------------------------------------- 
// Data 
//------------------------------------------- 
Data :
	"data" name=ID 
	"scheme" scheme=STRING
	("host" host=STRING)? 
	("port" port=STRING)? 
	("path" path=STRING)? ;
	
//------------------------------------------- 
// Application 
//------------------------------------------- 
Application :
	"Application" name=ID 
	"avd" avd=INT 
	("workdir" workDir=ID )? 
	"package" packageName=URI 
	"entryActiviy" mainActivity=[Activity] 
	"target" target=INT
	"path" path=ID ;

URI : 
	ID "." RestUri ; 

RestUri : 
	ID | URI ;
